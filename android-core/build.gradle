ext {
    kitDescription = 'Core mParticle SDK supporting only server-side integrations.'
}

apply plugin: 'com.android.library'
apply from: '../scripts/maven.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    String url = "\"\"";
    String identityUrl = "\"\""
    String configurationUrl = "\"\"";
    try {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        if (properties.stringPropertyNames().contains('mp.url')) {
            url = properties.getProperty('mp.url')
        }
        if (properties.stringPropertyNames().contains('mp.identityurl')) {
            identityUrl = properties.getProperty('mp.identityurl');
        }
        if (properties.stringPropertyNames().contains('mp.configurl')) {
            configurationUrl = properties.getProperty('mp.configurl')
        }
    } catch (Exception e) {

    }

    def gitHash = "git rev-parse --short HEAD".execute().text.trim()

    if (gitHash == null || gitHash.length() == 0) {
        gitHash = System.getenv("MP_GIT_SHA").substring(0, 7)
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 28
        versionName project.version.toString().replace("-SNAPSHOT", "")
        versionCode Integer.parseInt(new Date().format('yyyyMMdd'))

        buildConfigField "String", "GIT_SHA", "\"$gitHash\""
        buildConfigField "String", "MP_URL", url
        buildConfigField "String", "MP_IDENTITY_URL", identityUrl
        buildConfigField "String", "MP_CONFIG_URL", configurationUrl
        buildConfigField "Boolean", "MP_DEBUG", "false"
        buildConfigField "String", "SCHEME", "\"https\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            //multidex is required for instrumented test builds
            multiDexEnabled true
            buildConfigField "Boolean", "MP_DEBUG", "true"
            buildConfigField "String", "MP_URL", "\"localhost:8080\""
            buildConfigField "String", "MP_CONFIG_URL", "\"localhost:8080\""
            buildConfigField "String", "MP_IDENTITY_URL", "\"localhost:8080\""
            buildConfigField "String", "SCHEME", "\"http\""
        }
        release {
            minifyEnabled true
            proguardFiles 'proguard.pro'
            consumerProguardFiles 'consumer-proguard.pro'
            //buildConfigField "Boolean", "MP_DEBUG", "true"
        }
    }
}

task coreSdkJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        source += 'build/generated/source/buildConfig/release/'
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        title = 'mParticle Android SDK API Reference'
        failOnError false
        exclude {
            String filePath = it.toString()
            filePath.contains('/com/mparticle/internal/') ||
                    filePath.contains('/com/mparticle/kits/')
        }
        getOptions().setNoTimestamp(true)
}

dependencies {
    compileOnly 'com.google.android.gms:play-services-gcm:10.2.1'
    compileOnly 'com.google.android.gms:play-services-ads:10.2.1'
    compileOnly 'com.google.firebase:firebase-messaging:10.2.1'
    compileOnly 'com.android.support:support-v4:27.1.0'
    compileOnly 'com.android.installreferrer:installreferrer:[1.0, )'
    compileOnly 'com.google.android.instantapps:instantapps:[1.0, )'

    //the junit tests seem to explicitly need this dependency while
    //working in Android studio, but from the command line it's not necessary
    testImplementation project(':android-core')
    testImplementation 'junit:junit:4.12'
    testImplementation  files('libs/java-json.jar')
    testImplementation 'org.powermock:powermock-module-junit4:1.6.2'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.2'
    testImplementation 'org.powermock:powermock-core:1.6.2'
    testImplementation 'com.android.support:support-v4:27.1.1'
    testImplementation project(':testutils')

    androidTestImplementation project(':testutils')
}

configurations {
    lintJarImport
}

dependencies {
    lintJarImport project(path: ":custom-lint-rules", configuration: "lintChecks")
}


task copyLintJar(type: Copy) {

    from(configurations.lintJarImport) {
        rename {
            String fileName ->
                'lint.jar'
        }
    }
    into 'build/intermediates/lint/'
}


project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

